varnishtest "obj.ttl relative/absolute"

server s1 {
	rxreq
	txresp -hdr "Cache-Control: max-age=23" -hdr "Age: 4" -bodylen 40
} -start

varnish v1 -vcl+backend {
	sub vcl_fetch {
		set beresp.ttl = 10s;
		set req.http.foo = beresp.ttl;
		set req.http.bar = "xxx";
	}
	sub vcl_hit {
		set req.http.foo = obj.ttl;
		set obj.ttl = 7s;
		set obj.grace = 120s;
		set obj.keep = 1h;
		set req.http.bar = obj.ttl;
	}
	sub vcl_deliver {
		set resp.http.foo = req.http.foo;
		set resp.http.bar = req.http.bar;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.bodylen == 40
	expect resp.http.foo == 10.000
	expect resp.http.bar == "xxx"

	delay 2
	txreq
	rxresp
	expect resp.bodylen == 40
	# XXX: should be: < 8
	expect resp.http.foo != 10.000
	expect resp.http.bar == 7.000

	delay 2
	txreq
	rxresp
	expect resp.bodylen == 40
	# XXX: should be: < 5
	expect resp.http.foo != 7.000
	expect resp.http.bar == 7.000
} -run
